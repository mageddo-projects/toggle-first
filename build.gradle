plugins {
	id 'java'
	id "io.codearte.nexus-staging" version "0.20.0"
	id "de.marcphilipp.nexus-publish" version "0.2.0"
}

nexusStaging {
	packageGroup = 'com.mageddo'
	username = project.findProperty("ossrhUsername")
	password = project.findProperty("ossrhPassword")
}

subprojects {

	apply plugin: 'java'
	apply plugin: 'maven-publish'
	apply plugin: "de.marcphilipp.nexus-publish"

	repositories {
		mavenLocal()
		mavenCentral()
	}

	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8

	compileTestJava {
		sourceCompatibility = JavaVersion.VERSION_11
		targetCompatibility = JavaVersion.VERSION_11
	}

	tasks.withType(JavaCompile) {
		options.encoding = 'UTF-8'
	}

	task javadocJar(type: Jar) {
		classifier = 'javadoc'
		from javadoc
	}

	task sourcesJar(type: Jar) {
		classifier = 'sources'
		from sourceSets.main.allSource
	}

	artifacts {
		archives javadocJar, sourcesJar
	}

	if (project.hasProperty("signing.keyId")) {
		apply plugin: 'signing'
		signing {
			sign configurations.archives
		}
	}

	publishing {
		publications {
			mavenJava(MavenPublication) {
				pom {
					name = project.name
					packaging = 'jar'
					description = project.description
					url = "${githubRepoUrl}/tree/master/" + project.name

					scm {
						connection = "${githubRepoUrl}.git"
						developerConnection = "${githubRepoUrl}.git"
						url = "${githubRepoUrl}/tree/master/" + project.name
					}

					licenses {
						license {
							name = 'The Apache License, Version 2.0'
							url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						}
					}

					developers {
						developer {
							id = 'mageddo'
							name = 'Elvis Souza'
							email = 'edigitalb@gmail.com'
						}
					}

				}
				from(components.java)
			}
		}
	}

}

project(":toggle-first-core") {
	dependencies {

		compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
		compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
		compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.6'
		compile group: 'com.mageddo', name: 'commons', version: '1.2.0'
		compileOnly group: 'org.springframework.boot', name: 'spring-boot-starter', version: '2.0.4.RELEASE'

		testCompile group: 'junit', name: 'junit', version: '4.12'
		testCompile group: 'org.mockito', name: 'mockito-core', version: '2.27.0'

	}
}

project(":toggle-first-repo-jdbc") {
	dependencies {

		compile(project(':toggle-first-core'))
		compile group: 'org.flywaydb', name: 'flyway-core', version: '5.2.4'

		testCompile group: 'junit', name: 'junit', version: '4.12'
		testCompile group: 'org.hsqldb', name: 'hsqldb', version: '2.4.1'
	}
}

task version(){
	println(project.version)
}
